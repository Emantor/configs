#!/usr/bin/zsh -f
zmodload -i zsh/parameter || return 1

prompt_phoenix_help () {
    setopt localoptions nocshnullcmd noshnullcmd
    [[ $ZSH_VERSION < 5.0.0 ]] &&
	print 'Requires ZSH_VERSION 5.0.0'$'\n'
    <<-\EOF
        My basic prompt. A lot is stolen from fellow hackerspace member
        Valodim. The source can be found at http://github.com/valodim/zshrc.
	EOF
}

prompt_phoenix_vcsstyles () {
    # vcs_info configuration
    echo $nvcs_prompt
    zstyle ':vcs_info:*:phoenix:*' unstagedstr '●'
    zstyle ':vcs_info:*:phoenix:*' stagedstr '✔'
    zstyle ':vcs_info:*:phoenix:*' actionformats '(%F{10}%s->%F{3}%r%f|%F{14}%b%f%)%F{9}%u%F{10}%c%f(%F{9}%a%f):%F{3}%S%f'
    zstyle ':vcs_info:*:phoenix:*' formats '(%F{10}%s->%F{3}%r%f|%F{14}%b%f%)%F{9}%u%F{10}%c%f%:%F{3}%S%f'
    zstyle ':vcs_info:*:phoenix:*' nvcsformats '%F{yellow}%3~%f'
    zstyle ':vcs_info:(sv[nk]|bzr):*:phoenix:*' branchformat '%b%F{1}:%F{3}%r'
}

prompt_phoenix_precmd () {
    setopt localoptions noksharrays unset

    prompt_bits=

    if [[ -w $PWD ]]; then
        prompt_bits="%B%F{blue}"
    elif [[ -r $PWD ]]; then
        prompt_bits="%B%F{yellow}"
    else
        prompt_bits="%B%F{red}"
    fi
    [[ -O $PWD ]] && prompt_bits+=":%b%f" || prompt_bits+=".%b%f"

    # On Xterm, Rxvt and screen, print command/path as window title,
    # otherwise exec only vcs_info
    case $TERM in
        xterm*|rxvt*|screen*)
            print -Pn "\e]0;$TERM:%m:%3~\a";
            ;;
    esac

    local screens=$(screen -ls 2> /dev/null | grep -c Detach )
    local jobs=$(jobs | wc -l )

    if [[ $screens == "0" ]]; then
        screenbits=""
    else
        screenbits="%F{yellow}[%F{white}s%F{grey}:%F{yellow}$screens]%f"
    fi

    if [[ $jobs == "0" ]]; then
        jobbits=""
    else
        jobbits="%F{white}[%F{white}j%F{grey}:%F{yellow}$jobs%F{white}]%f"
    fi

    vcs_info 'phoenix'
}

prompt_phoenix_preexec () {
    case $TERM in
        rxvt*|xterm*|screen*)
            print -Pn "\e]0;$TERM:$1\a"
            ;;
    esac
}

prompt_phoenix_ps1 () {
    setopt localoptions noksharrays extendedglob

    local host_color
    if ! zstyle -s ":prompt:phoenix:ps1:${HOST}" host-color host_color; then
        # get a number from the sha1 hash of the hostname
        host_color=${${"$(sha1sum <<< $HOST)"//[a-z]#/}[1,16]}
        # divide by 256/88/8 whatever
        host_color=$(( host_color % $(echoti colors) ))
    fi

    # hide username if it's my regular one (anyone else might wanna change this)
    local user=""
    if ! zstyle -t ':prompt:phoenix:ps1' hide-user; then
        user="%(!.%F{red}root%f@.%n@)"
    fi

    local host="%F{${host_color}%}%m%f"
    local exstate="[%{$fg_no_bold[yellow]%}%?%{$reset_color%}]"
    local date="%{$fg_no_bold[yellow]%}%D{%H:%M:%S}%{$reset_color%}"
    local gitinfo='${vcs_info_msg_0_}%#'



    prompt_bits="%F{blue}::%f"

    PROMPT="${user}\${screenbits}\${jobbits}${host}\${prompt_bits}${gitinfo}"
    RPROMPT="${date}|${exstate}"
}

prompt_phoenix_setup () {
    setopt localoptions noksharrays unset

    # load zsh/termcap
    zmodload zsh/terminfo

    # load vcs_info styles
    autoload -Uz vcs_info
    prompt_phoenix_vcsstyles

    # set up left-side prompt
    prompt_phoenix_ps1

    # we know add-zsh-hook has been loaded by promptinit
    add-zsh-hook precmd prompt_phoenix_precmd

    # Prexec Hook
    add-zsh-hook preexec prompt_phoenix_preexec

    # no multiline so we don't need cr, but we do need both % and $ expansion
    prompt_opts=( percent subst )

    return 0
}

[[ -o kshautoload ]] || prompt_phoenix_setup "$@"
# vim: set filetype=zsh:
